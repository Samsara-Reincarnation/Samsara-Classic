actor " Chainsaw " : Chainsaw replaces Chainsaw
{
    Weapon.SlotNumber 1
    Weapon.SlotPriority 0
    Decal BulletChip
    Weapon.UpSound "doomguy/sawup"
    Weapon.ReadySound "doomguy/sawidle"
    DamageType "Chainsaw"
    +INVENTORY.UNDROPPABLE
    Obituary "$SAMSARA_DOOMGUY_OB_SLOT1"
	Inventory.RestrictedTo "DoomguyPlayer"
	Tag "Chainsaw"
    States
    {
      Ready:
		DSAW C 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaReady")
        DSAW C 0 A_StopSound
		DSAW C 0 A_PlaySound("doomguy/sawidle", CHAN_WEAPON)
		DSAW CEFG 2 A_WeaponReady
        loop
		
	  VanillaReady:
        DSAW C 0 A_StopSound
		DSAW C 0 A_PlaySound("doomguy/sawidle", CHAN_WEAPON)
        DSAW CD 4 A_WeaponReady
        Goto Ready

      Deselect:
        DSAW C 1 A_Lower
        loop

      Select:
        DSAW C 1 A_Raise
        loop

      Fire:
		TNT1 A 0 A_JumpIfInventory("VanillaDoom", 1, "VanillaFire")
        TNT1 A 0 A_JumpIfInventory("PowerChainsawBerserk", 1, "FireBerserk")
        DSAW A 2 A_Saw("doomguy/sawfull", "doomguy/sawhit", 2, "DoomBulletPuff") 
        DSAW B 2
		DSAW A 2 A_Saw("doomguy/sawfull", "doomguy/sawhit", 2, "DoomBulletPuff") 
		DSAW B 2 
		DSAW B 0 A_ReFire
        goto Ready
		
	  VanillaFire:
	    TNT1 A 0 A_JumpIfInventory("PowerChainsawBerserk", 1, "VanillaFireBerserk")
        DSAW AB 4 A_Saw("doomguy/sawfull", "doomguy/sawhit", 2, "DoomBulletPuff") 
        DSAW B 0 A_ReFire
        goto Ready

      FireBerserk:
        DSAW A 2 A_Saw("doomguy/sawfull", "doomguy/sawhit", 7, "DoomBulletPuff") 
        DSAW B 1
		DSAW A 2 A_Saw("doomguy/sawfull", "doomguy/sawhit", 7, "DoomBulletPuff") 
		DSAW B 1
		DSAW B 0 A_ReFire
        goto Ready
		
      VanillaFireBerserk:
        DSAW AB 3 A_Saw("doomguy/sawfull", "doomguy/sawhit", 7, "DoomBulletPuff") 
        DSAW B 0 A_ReFire
        goto Ready

      Spawn:
        WSAW A -1
        stop
    }
}

actor PowerChainsawBerserk : Powerup//PowerDamage
{
    +INVENTORY.HUBPOWER
    Powerup.Duration 0x7FFFFFFF
    //DamageFactor "Chainsaw", 10.0 // Yeah, that turned out to not work for some reason.
}
